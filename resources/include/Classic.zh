//Classic.zh

//Include Headers
#include "ffcscript.zh"
#include "stdExtra.zh"
#include "ghost.zh"
#include "tango.zh"

namespace classic_zh
{
	//Version Info:
	const float VERSION 			= 2.14;
	const int DATE_DAY 			= 01;
	const int DATE_MONTH 			= 10;
	const int DATE_YEAR 			= 2020;
	const int HOUR				= 5;
	const int MINUTE			= 3;
	
	//System Limits
	const int MAX_LEVELS			= 512;
	
	//Basic Settings
	
	//Invisible Combo for FFCs
	const int CMB_INVISIBLE 		= 1;
	
	//Max distance to signpost, in pixels
	const int SIGNPOST_DIST 		= 16;
	
	//Boss Explosion Default Settings
	const int BOSS_EXPLODE_SPRITE 		= 88;
	const int BOSS_EXPLODE_DUR 		= 150;
	const int BOSS_EXPLODE_SFX 		= 61;
	const int BOSS_EXPLODE_DELAY 		= 16;
	const int BOSS_EXPLODE_RANDOM_MIN 	= -10;
	const int BOSS_EXPLODE_RANDOM_MAX 	= 10;

	//Boss Music Default Settings
	const int TRK_BOSS 			= 25;
	const int MIDI_VICTORY 			= 1;
	const int TILE_FIREWORK 		= 10140;

	//Boss Chain Screen->D[] Registers
	const int REG_BOSS_EXPLODING 		= 1;
	const int REG_BOSS_DEAD 		= 2;
	const int REG_BOSS_DROPPED_HEART 	= 3;
	const int REG_BOSS_DROPPED_TRIFORCE 	= 4;

	//Returns the current version of Classic.zh
	float getVersion() { return VERSION; }
	
	//Functions
	//Returns the datestamp of the current version of Classic.zh as a compound float in format DDMM.YYYY
	float getDate() 
	{
		int date = DATE_MONTH;
		date += DATE_YEAR/10000;
		date += ( DATE_DAY * 100 );
		return date;
	}
	//Returns the timestamp of classic.zh as a compound float in the format of HH.MM
	float getTime() 
	{
		int tm = MINUTE;
		tm += HOUR*100;
	}
	//Returns true if an enemy is a traditional ZC Boss.
	bool isBoss(npc n)
	{
		switch(n->Type)
		{
			case NPCT_AQUAMENTUS: return true;
			case NPCT_MOLDORM: return true;
			case NPCT_DODONGO: return true;
			case NPCT_GLEEOK: return true;
			case NPCT_DIGDOGGER: return true;
			case NPCT_GOHMA: return true;
			case NPCT_LANMOLA: return true;
			case NPCT_MANHANDLA: return true;
			case NPCT_PATRA: return true;
			case NPCT_GANON: return true;
			default: return false;
		}
	}
	//Returns if there are enemy NPCs alive on the current screen.
	bool ScreenGuyAlive()
	{
		bool alive = false; 
		for ( int q = Screen->NumNPCs(); q > 0; --q )
		{
			npc n = Screen->LoadNPC(q); 
			if ( n->Type == NPCT_GUY )
			{
				alive = true;
			}
		}
		return alive;
	}

}

//Global Constants

//Items
const int I_HALFMAGIC 					= 144;
const int I_LEARNSLASH 					= 143;

const int I_MAGICBOSSKEY 				= 146;
const int I_MAGICMAP 					= 147;
const int I_MAGICCOMPASS 				= 148;
//Need item consts for AutoLantern and 4_8_Way_Movement items. Set them up in the qst.dat!

//Include Scripts
#include "npc_scripts/bobomb.zs"
#include "npc_scripts/daira.zs"
#include "eweapon_scripts/daira_axe.zs"
#include "npc_scripts/bobomb.zs"
#include "npc_scripts/daira.zs"
#include "eweapon_scripts/daira_axe.zs"
#include "eweapon_scripts/ewZigZag.zs"
#include "lweapon_scripts/lwZigZag.zs"
#include "lweapon_scripts/lwBombArrow.zs"
#include "lweapon_scripts/replaceOnDeath.zs"
#include "lweapon_scripts/firerod.zs"
#include "lweapon_scripts/mobileBait.zs"

//Scripts
//Automatically lights dark rooms as a passive item.
itemdata script Z2_Lantern
{
	void run()
	{
		while(1)
		{
			unless(Screen->Lit) { Screen->Lit = true; } //Is this lighter than just writing Lit = true every frame?
			Waitframe();
		}
	}
}

itemdata script toggle_4_8_way_movement
{
	void run()
	{
		Game->FFRules[qr_LTTPWALK] = !Game->FFRules[qr_LTTPWALK];
	}
}

itemdata script LearnSlash
{
	void run()
	{
		Game->Generic[GEN_CANSLASH] = 1;
	}
}

itemdata script ItemMessage
{
	void run(int m)
	{
		Screen->Message(m);
	}
}

itemdata script ItemSound
{
	void run(int s)
	{
		Audio->PlaySound(s);
	}
}

ffc script BossMusic
{
	void run(int reg, int origtile, int mid, int trk)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( Screen->D[reg] >= classic_zh::REG_BOSS_DEAD ) Quit(); //Don't play victory music if we return to the screen.
		origtile = ( origtile > 0 ) ? origtile : classic_zh::TILE_FIREWORK;
		trk = ( trk > 0 ) ? trk : classic_zh::TRK_BOSS;
		mid = ( mid > 0 ) ? mid : classic_zh::MIDI_VICTORY;
		Waitframes(5);
		Game->PlayEnhancedMusic("Classic.nsf",trk);
		while(EnemiesAlive()) Waitframe();
		//for ( int q = Screen->NumLWeapons(); q > 0; --q ) //the number changes, so we need to refresh...
		for ( int q = 1; q < Screen->NumLWeapons(); ++q ) 
		{
			lweapon isFirework = Screen->LoadLWeapon(q);
			if ( isFirework->OriginalTile == origtile ) 
			{
				while(isFirework->isValid()) Waitframe();
				
			}
		}
		Audio->PlayMIDI(mid);
		for ( int q = 0; q < 601; ++q ) Waitframe();
		Game->PlayMIDI(Game->DMapMIDI[Game->GetCurDMap()]);	
		Screen->D[reg] = classic_zh::REG_BOSS_DEAD;
	}
}

ffc script BossExplode
{
	void run(int reg, int spr, int dur, int sfx, int delay, int rmin, int rmax, int enemy_override)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		int type;
		spr = ( spr > 0 ) ? spr : classic_zh::BOSS_EXPLODE_SPRITE;
		dur = ( dur > 0 ) ? dur : classic_zh::BOSS_EXPLODE_DUR;
		sfx = ( sfx > 0 ) ? sfx : classic_zh::BOSS_EXPLODE_SFX;
		delay = ( delay > 0 ) ? delay :  classic_zh::BOSS_EXPLODE_DELAY;
		rmin = ( rmin > 0 ) ? rmin : classic_zh::BOSS_EXPLODE_RANDOM_MIN;
		rmax = ( rmax > 0 ) ? rmax : classic_zh::BOSS_EXPLODE_RANDOM_MAX;
		Waitframes(5);
		npc boss; int x; int y; int width; int height; bool alive = true;
		for ( int q = Screen->NumNPCs(); q > 0; --q )
		{
			npc n = Screen->LoadNPC(q);
			if ( classic_zh::isBoss(n) || n->ID == enemy_override ) 
			{
				boss = n; 
				width = n->TileWidth;
				height = n->TileHeight;
				x = n->X;
				y = n->Y;
				type = boss->Type;
				break;
			}
			
		}
		unless ( (boss->isValid()) ) Quit();
		while(alive)
		{
			while(boss->isValid()) //boss->HP > 0)
			{
				x = boss->X; Trace(x);
				y = boss->Y; Trace(y);
				Waitframe();
				continue;
			}
			//Check for more pieces of the boss
			if ( EnemiesAlive() )
			{
				//Find the next piece of it
				for ( int q = Screen->NumNPCs(); q > 0; --q )
				{
					npc n = Screen->LoadNPC(q);
					if ( n->Type == type ) 
					{
						boss = n; 
						width = n->TileWidth;
						height = n->TileHeight;
						x = n->X;
						y = n->Y;
						type = boss->Type;
						break;
					}
					
				}
			}
			else { alive = false; break; }
			Waitframe();
		}
			
		Screen->D[reg] = classic_zh::REG_BOSS_EXPLODING;
		for ( int q = dur; q > 0; --q )
		{
			//do explosions
			Audio->PlaySound(sfx);
			lweapon l = Screen->CreateLWeapon(LW_SPARKLE);
			l->CollDetection = false;
			l->UseSprite(spr);
			l->X = x+(Rand((rmin-width), rmax+width));
			l->Y = y+(Rand((rmin-height), rmax+height));
			//wait n frames between explosions
			for ( int w = 0; q < delay; ++w )
			{
				Waitframe(); //--q; //Increment the main loop at the same time.
			}
			Waitframe();
		}
		Screen->D[reg] = classic_zh::REG_BOSS_DEAD;
		Quit();
		
	}
}

ffc script SignPost
{
	void run(int m, int max_dist)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( max_dist < 1 ) max_dist = classic_zh::SIGNPOST_DIST;
		while(1)
		{
			if ( Below(this) )
			{
				if ( Facing(this) )
				{
					if ( DistXY(this, max_dist) )
					{
						if ( Link->PressA || Link->PressB )
						{
							Screen->Message(m);
						}
					}
				}
			}
			Waitframe();
		}
	}
}

//ffc script PermSecrets
//{
//    void run(int reg)
//    {
//        while(1)
//        {
//            if ( !Screen->D[reg] )
//            {
//                if ( Screen->State[ST_SECRET] ) 
//                {
//                    Screen->D[reg] = 1;
//                }
//            }
//            else 
//            {
//                Screen->TriggerSecrets();
//                Quit();
//            }
//            Waitframe();
//        }
//    }
//}

ffc script ChangeRoomGuy
{
	void run(int t, int cs)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( t < 0 ) Quit();
		Waitframes(4);
		for ( int q = Screen->NumNPCs(); q > 0; --q )
		{
			npc g = Screen->LoadNPC(q);
			if ( g->Type == NPCT_GUY )
			{
				if ( g->ID != NPC_FIRE )
				{
					g->OriginalTile = t;
					if ( cs > 0 ) 
						g->CSet = cs;
					break;
				}
			}
		}
	}
}

ffc script PermBlockSecrets
{
	void run(int reg)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		int cnt;
		while(1)
		{
			if ( Screen->D[reg] )
			{
				Screen->State[ST_SECRET] = true;
				Screen->TriggerSecrets();
				Audio->PlaySound(SFX_SECRET);
				Quit();
			}
			cnt = 0; 
			for ( int q = 0; q < 176; ++q )
			{
				if ( Screen->ComboF[q] == CF_BLOCKTRIGGER ) ++cnt;
				if ( Screen->ComboI[q] == CF_BLOCKTRIGGER ) ++cnt;
			}
			unless ( cnt ) 
			{
				Screen->State[ST_SECRET] = true;
				Screen->D[reg] = 1; 
				break;
			}
			Waitframe(); 
		}
				
		//count the number of block triggers on the screen
		//then, if there are none, set the screen state
		
	}
}

ffc script PermBlockShutters
{
	void run(int reg)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		int cnt;
		while(1)
		{
			if ( Screen->D[reg] )
			{
				Audio->PlaySound(SFX_SECRET);
				//Screen->Flags[SF_SECRETS] &= ~1;
				for ( int q = 0; q < 4; ++q )
				{
					if ( Screen->Door[q] == D_SHUTTER ) Screen->Door[q] = D_OPEN;
				}
				Quit();
			}
			cnt = 0; 
			for ( int q = 0; q < 176; ++q )
			{
				if ( Screen->ComboF[q] == CF_BLOCKTRIGGER ) ++cnt;
				if ( Screen->ComboI[q] == CF_BLOCKTRIGGER ) ++cnt;
			}
			unless ( cnt ) 
			{
				Screen->State[ST_SECRET] = true;
				Screen->D[reg] = 1; 
				break;
			}
			Waitframe(); 
		}
				
		//count the number of block triggers on the screen
		//then, if there are none, set the screen state
		
	}
}

global script Active
{
    void run()
    {
        StartGhostZH();
        Tango_Start();
	    
        while(true)
        {
            UpdateGhostZH1();
	    Tango_Update1();
            Waitdraw();
            UpdateGhostZH2();
	    Tango_Update2();
            Waitframe();
        }
    }
}

ffc script DropTRIFORCE
{
	void run(int reg, int min)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( Screen->D[reg] >= classic_zh::REG_BOSS_DROPPED_TRIFORCE ) Quit();
		if ( min < 1 ) min = classic_zh::REG_BOSS_DROPPED_HEART;
		item hc = Screen->CreateItem(I_TRIFORCE);
		int yy;
		hc->X = 1; hc->Y = 1; hc->DrawYOffset = -10000; hc->HitYOffset = -10000; hc->Z = 256;
		while ( Screen->D[reg] < min ) { hc->Z = 256; Waitframe(); }
		//boss is dead. Position the heart container over Link.
		hc->X = (Clamp(Link->X, 0, 250)); yy = (Clamp(Link->Y, 0, 150)); //Don't drop partway offscreen.
		hc->Y = yy;
		hc->DrawYOffset = 0; hc->HitYOffset = 0; 
		lweapon shadow = Screen->CreateLWeapon(LW_SCRIPT1);
		shadow->UseSprite(50);
		shadow->X = hc->X; shadow->Y = yy+8;
		shadow->CollDetection = false;
		while(hc->isValid())
		{
			if ( hc->Z > 8 ) ++hc->Z; //Slow its descent. 
			if ( hc->Z == 0 && hc->Jump < 0.4) Remove(shadow);
			if ( Collision(hc) ) Screen->D[reg] = classic_zh::REG_BOSS_DROPPED_TRIFORCE; //If Link touches it. 
			Waitframe();
			
		}
		Screen->D[reg] = classic_zh::REG_BOSS_DROPPED_TRIFORCE;
	}	
}

ffc script DropHEARTCONTAINER
{
	void run(int reg, int min)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( Screen->D[reg] >= classic_zh::REG_BOSS_DROPPED_HEART ) Quit();
		if ( min < 1 ) min = classic_zh::REG_BOSS_DEAD;
		item hc = Screen->CreateItem(I_HEARTCONTAINER);
		int yy;
		hc->X = 1; hc->Y = 1; hc->DrawYOffset = -10000; hc->HitYOffset = -10000; hc->Z = 256;
		while ( Screen->D[reg] < min ) { hc->Z = 256; Waitframe();}
		//boss is dead. Position the heart container over Link.
		hc->X = (Clamp(Link->X, 0, 250)); yy = (Clamp(Link->Y, 0, 150)); //Don't drop partway offscreen.
		hc->Y = yy;
		hc->DrawYOffset = 0; hc->HitYOffset = 0; 
		lweapon shadow = Screen->CreateLWeapon(LW_SCRIPT1);
		shadow->UseSprite(50);
		shadow->X = hc->X; shadow->Y = yy+8;
		shadow->CollDetection = false;
		while(hc->isValid())
		{
			if ( hc->Z > 8 ) ++hc->Z; //Slow its descent. 
			if ( hc->Z == 0 && hc->Jump < 0.4) Remove(shadow); 
			Waitframe();
		}
		Screen->D[reg] = classic_zh::REG_BOSS_DROPPED_HEART;
	}	
}


//Collect script for Magic Boss Key--works in a levels
itemdata script MagicBossKey
{
	void run()
	{
		for ( int q = 0; q < classic_zh::MAX_LEVELS; ++q )
		{
			Game->LItems[q] |= LI_BOSSKEY;
		}
	}
}

//Collect script for Magic Map--works in a levels
itemdata script MagicMap
{
	void run()
	{
		for ( int q = 0; q < classic_zh::MAX_LEVELS; ++q )
		{
			Game->LItems[q] |= LI_MAP;
		}
	}
}

//Collect script for Magic Compass-works in all levels
itemdata script MagicCompass
{
	void run()
	{
		for ( int q = 0; q < classic_zh::MAX_LEVELS; ++q )
		{
			Game->LItems[q] |= LI_COMPASS;
		}
	}
}

//Allows showing a uniue string for each of the four HC pieces. 
itemdata script CollectHeartContainer
{
	void run(int m)
	{
		Screen->Message(m+Game->Generic[GEN_HEARTPIECES]);
	}
}

ffc script ScreenGuyMusic
{
	void run(int reg, int mid, int trk)
	{
		this->Data = classic_zh::CMB_INVISIBLE;
		if ( Screen->D[reg] >= classic_zh::REG_BOSS_DEAD ) Quit(); //Don't play victory music if we return to the screen.
		if ( trk < 1 ) trk = classic_zh::TRK_BOSS;
		if ( mid < 1 ) mid = classic_zh::MIDI_VICTORY;
		int filenm[]="Classic.nsf";
		Waitframes(5);
		Game->PlayEnhancedMusic(filenm,trk);
		while(classic_zh::ScreenGuyAlive()) Waitframe();
		Game->PlayMIDI(Game->DMapMIDI[Game->GetCurDMap()]);	
	}
}