// tango.zh
// Version 1.2.0


const int __TANGO_CONTROL_CODES_ENABLED = 1;

// ZC stores control codes one higher than the code, e.g. \15 is really 16
const int __TANGO_SCC_TEXT_COLOR = 2;
const int __TANGO_SCC_TEXT_SPEED = 3;
const int __TANGO_SCC_GOTO_IF_SCREEN_D = 4;
const int __TANGO_SCC_GOTO_IF_RANDOM = 5;
const int __TANGO_SCC_GOTO_IF_ITEM = 6;
const int __TANGO_SCC_GOTO_IF_COUNTER = 7;
const int __TANGO_SCC_GOTO_IF_COUNTER_PERCENT = 8;
const int __TANGO_SCC_GOTO_IF_TRIFORCE = 9;
const int __TANGO_SCC_GOTO_IF_TRIFORCE_COUNT = 10;
const int __TANGO_SCC_COUNTER_UP = 11;
const int __TANGO_SCC_COUNTER_DOWN = 12;
const int __TANGO_SCC_COUNTER_SET = 13;
const int __TANGO_SCC_COUNTER_UP_PERCENT = 14;
const int __TANGO_SCC_COUNTER_DOWN_PERCENT = 15;
const int __TANGO_SCC_COUNTER_SET_PERCENT = 16;
const int __TANGO_SCC_ADD_ITEM = 17;
const int __TANGO_SCC_REMOVE_ITEM = 18;
const int __TANGO_SCC_PLAY_SOUND = 21;
const int __TANGO_SCC_PLAY_MIDI = 22;

int __Tango_RunStringControlCode(int pos)
{
    int code=__Tango_Buffer[pos];
    int numArgs=__Tango_GetNumSCCArgs(code);
    int args[3]={ 0, 0, 0 };
    pos++;
    
    for(int i=0; i<numArgs; i++)
    {
        args[i]=__Tango_Buffer[pos];
        pos++;
    }
    
    if(code<11)
    {
        if(code==__TANGO_SCC_TEXT_COLOR)
        {
            __Tango_SetVar(__TANGO_VAR_CSET, args[0]);
            __Tango_SetVar(__TANGO_VAR_COLOR, args[1]);
        }
        else if(code==__TANGO_SCC_TEXT_SPEED)
            __Tango_SetVar(__TANGO_VAR_SPEED, args[0]);
        else if(code==__TANGO_SCC_GOTO_IF_SCREEN_D)
        {
            if(Screen->D[args[0]]>=args[1])
                __Tango_FuncGoto(args[2]);
        }
        else if(code==__TANGO_SCC_GOTO_IF_RANDOM)
        {
            if(Rand(args[0])==0)
                __Tango_FuncGoto(args[1]);
        }
        else if(code==__TANGO_SCC_GOTO_IF_ITEM)
        {
            if(Link->Item[args[0]])
                __Tango_FuncGoto(args[1]);
        }
        else if(code==__TANGO_SCC_GOTO_IF_COUNTER)
        {
            if(Game->Counter[args[0]]>=args[1])
                __Tango_FuncGoto(args[2]);
        }
        else if(code==__TANGO_SCC_GOTO_IF_COUNTER_PERCENT)
        {
            if(Game->Counter[args[0]]/Game->MCounter[args[0]]*100>=args[1])
                __Tango_FuncGoto(args[2]);
        }
        else if(code==__TANGO_SCC_GOTO_IF_TRIFORCE)
        {
            if((Game->LItems[args[0]]&LI_TRIFORCE)!=0)
                __Tango_FuncGoto(args[1]);
        }
        else // __TANGO_SCC_GOTO_IF_TRIFORCE_COUNT
        {
            if(NumTriforcePieces()>=args[0])
                __Tango_FuncGoto(args[1]);
        }
    }
    else // code>=11
    {
        if(code==__TANGO_SCC_COUNTER_UP)
            Game->Counter[args[0]]=Min(Game->Counter[args[0]]+args[1],
              Game->MCounter[args[0]]);
        else if(code==__TANGO_SCC_COUNTER_DOWN)
            Game->Counter[args[0]]=Max(Game->Counter[args[0]]-args[1], 0);
        else if(code==__TANGO_SCC_COUNTER_SET)
            Game->Counter[args[0]]=Clamp(args[1], 0, Game->MCounter[args[0]]);
        else if(code==__TANGO_SCC_COUNTER_UP_PERCENT)
        {
            int amount=(Game->MCounter[args[0]]*args[1]/100)<<0;
            Game->Counter[args[0]]=Min(Game->Counter[args[0]]+amount,
              Game->MCounter[args[0]]);
        }
        else if(code==__TANGO_SCC_COUNTER_DOWN_PERCENT)
        {
            int amount=(Game->MCounter[args[0]]*args[1]/100)<<0;
            Game->Counter[args[0]]=Max(Game->Counter[args[0]]-amount, 0);
        }
        else if(code==__TANGO_SCC_COUNTER_SET_PERCENT)
        {
            int value=(Game->MCounter[args[0]]*args[1]/100)<<0;
            Game->Counter[args[0]]=Clamp(value, 0, Game->MCounter[args[0]]);
        }
        // The add and remove item codes don't affect counters...
        else if(code==__TANGO_SCC_ADD_ITEM)
            Link->Item[args[0]]=true;
        else if(code==__TANGO_SCC_REMOVE_ITEM)
            Link->Item[args[0]]=false;
        else if(code==__TANGO_SCC_PLAY_SOUND)
            Game->PlaySound(args[0]);
        else if(code==__TANGO_SCC_PLAY_MIDI)
            Game->PlayMIDI(args[0]);
    }
    
    return numArgs+1;
}

// Get the number of arguments used by a control code
int __Tango_GetNumSCCArgs(int code)
{
    if(code<9)
    {
        if(code==__TANGO_SCC_TEXT_COLOR)
            return 2;
        else if(code==__TANGO_SCC_TEXT_SPEED)
            return 1;
        else if(code==__TANGO_SCC_GOTO_IF_SCREEN_D)
            return 3;
        else if(code==__TANGO_SCC_GOTO_IF_RANDOM)
            return 2;
        else if(code==__TANGO_SCC_GOTO_IF_ITEM)
            return 2;
        else if(code==__TANGO_SCC_GOTO_IF_COUNTER)
            return 3;
        else // __TANGO_SCC_GOTO_IF_COUNTER_PERCENT
            return 3;
    }
    else if(code<17)
        return 2;
    else
        return 1;
}
